

#include "bit64.hbas"
#include "toc.hbas"
#include "seabass_stdlib.hbas"

//mutate pprint....
@wksht prnt[
    [
        @pprint [println itoa ARG1]
    ][
        ARG1
    ]
]

/*
    String testing...
*/

fn strtest1():
    str a
    str b
    a.new("Hello there!");
    b.copy(a);
    @prnt[
        /   "Here is A:"
        /   (a.d)
        /   "Here is B:"
        /   (b.d)
    ]
    b.dtor();
    a.new("Can you find the needle in the haystack, and replace it? well, can you? Can you find it in this haystack, or not???") ;
    b.new("in");
    str c
    c.new("i");
    a.replace(b,c);
    @prnt[
        /   "Here is A:"
        /   (a.d)
    ]
    if(a.len != strlen(a.d))
        @prnt[
            /   "Len failure!"
        ]
        sys_exit(1);
    end
    c.new("needle");
    b.new("needle in the");
    a.new("Can you find the needle in the haystack, and replace it? well, can you?") ;
    @prnt[
        /   "We find needle at:"
        /int (a.find(c))
    ]
    str* q = a.substr(a.find(c),b.len);
    if(q != 0 && q.d != 0)
        @prnt[
            /   "Here is Q:"
            /   (q.d)
        ]
        if(q.len != strlen(q.d))
            @prnt[
                /   "Len failure!"
            ]
            sys_exit(1);
        end
        q.dtor();
        free((byte*)q);
    else
        @prnt[
            /   "<ERR> It appears we failed to find it..."
        ]    
    end
    c.new("Cannot find this!");
    @prnt[
        /   "We find the unfindable needle at:"
        /int (a.find(c))
    ]
    u64 i
    for(i = 0, i < 100, i++)
        str qq
        qq.new("Will I leak?");
        a.new("Or perhaps I will");
        if(i == 73)
            str qqq
            qqq.new("But, perhaps, I will instead...")
            return
        end
    end
end

fn pub main()->i32:
    strtest1();
end

/*
    Our code generator...
*/
fn codegen codegen_main():
    cg_emitC(SEABASS_STDLIB_PREFIX);
end


